import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.cloud.tools.jib' version '3.4.5'
    id 'checkstyle'
    id 'com.github.spotbugs' version '6.4.2'
    id 'pmd'
}

jib {
    from {
        image = 'eclipse-temurin:21-jre'
    }
    to {
        image = 'spring-boot-jwt-auth'
    }

    container {
        ports = ['8080', '8080']
        workingDirectory = '/app'
        extraDirectories {
            paths {
                path {
                    from = './'
                    into = '/app'
                    includes = ['.env']
                }
            }
        }
    }
}

group = 'sc.snicky'
version = '0.0.1-SNAPSHOT'
description = 'spring-boot-jwt-auth'

ext {
    versions = [
            jsonwebtoken: "0.12.5",
            mapstruct  : "1.6.3"
    ]
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

pmd {
    ruleSetFiles = files("config/pmd/pmd-ruleset.xml")
    ruleSets = []
}

checkstyle {
    toolVersion = "10.12.4"
    configFile = file("config/checkstyle/checkstyle.xml")
}

tasks.withType(SpotBugsTask).configureEach {
    group = "linters"
    reports {
        xml.required.set(false)
        html.required.set(true)
        html.outputLocation.set(layout.buildDirectory.file("reports/spotbugs.html"))
    }
}

tasks.withType(Pmd).configureEach {
    group = "linters"
}

tasks.withType(Checkstyle).configureEach {
    group = "linters"
    reports {
        xml.required.set(false)
        html.required.set(true)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation "org.mapstruct:mapstruct:${versions.mapstruct}"
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation "io.jsonwebtoken:jjwt-api:${versions.jsonwebtoken}"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${versions.jsonwebtoken}"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:${versions.jsonwebtoken}"
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation('org.springframework.boot:spring-boot-starter') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    implementation "org.springframework.boot:spring-boot-starter-log4j2"
    modules {
        module("org.springframework.boot:spring-boot-starter-logging") {
            replacedBy("org.springframework.boot:spring-boot-starter-log4j2", "Use Log4j2 instead of Logback")
        }
    }


    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.mapstruct:mapstruct-processor:${versions.mapstruct}"

    runtimeOnly 'org.postgresql:postgresql'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}
